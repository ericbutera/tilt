load('ext://restart_process', 'docker_build_with_restart')

def lgtm(grafana_port=3000, auto_init=True, dashboard_dir=None):
    current_dir = os.path.dirname(__file__)

    if dashboard_dir:
        build_dir = os.path.join(current_dir, '.build')
        local('mkdir -p {}/dashboards'.format(build_dir))
        local('cp {} {}/'.format(os.path.join(current_dir, 'Dockerfile'), build_dir))
        local('cp {} {}/'.format(os.path.join(current_dir, 'prometheus.yaml'), build_dir))
        local('cp {} {}/'.format(os.path.join(current_dir, 'dashboards.yaml'), build_dir))
        local('cp {}/*.json {}/dashboards/ 2>/dev/null || true'.format(dashboard_dir, build_dir))

        local_resource(
            'lgtm-dashboards-sync',
            'cp {}/*.json {}/dashboards/ 2>/dev/null || true'.format(dashboard_dir, build_dir),
            deps=[dashboard_dir],
            labels=['services'],
            resource_deps=['lgtm'],
        )

        docker_build("lgtm-image", build_dir,
            live_update=[
                sync(build_dir + '/dashboards', '/otel-lgtm/grafana/conf/provisioning/dashboards')
            ])
    else:
        docker_build("lgtm-image", current_dir)

    yaml_path = os.path.join(current_dir, 'lgtm.yaml')
    k8s_yaml(yaml_path)
    k8s_resource(
        "lgtm",
        port_forwards=[
            port_forward(grafana_port, 3000, "grafana"),
            port_forward(9090, 9090, "prometheus"),
            port_forward(4317,4317, "collector - grpc"),
            port_forward(4318,4318, "collector - http"),
            port_forward(3100, 3100, "loki"),
        ],
        labels=["services"],
        auto_init=auto_init,
        trigger_mode=TRIGGER_MODE_MANUAL,
    )
